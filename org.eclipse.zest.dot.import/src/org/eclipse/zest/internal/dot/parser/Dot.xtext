/*******************************************************************************
 * Copyright (c) 2008, 2010 Michael Clay and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 * Contributors:
 *    Michael Clay - initial implementation, see bug 277380
 *    Fabian Steeg - minor updates for Helios Xtext
 *******************************************************************************/
grammar org.eclipse.zest.internal.dot.parser.Dot with org.eclipse.xtext.common.Terminals

generate dot "http://www.eclipse.org/zest/internal/dot/parser/Dot"

graphvizmodel:
	"graphs" "{"
		(graphs+=graph)*
	"}";

graph: "graph" (strict?="strict")? type=graphtype (name=DOT_ID)? "{" 
	(stmts+=stmt)* 
"}";

stmt: ( edge_stmt_node |  edge_stmt_subgraph |  node_stmt | attribute | attr_stmt | subgraph ) (";")?;

edge_stmt_node : node_id=node_id (edgeRHS+=edgeRHS)+ (attributes+=attr_list)*;

edge_stmt_subgraph : subgraph=subgraph  (edgeRHS=edgeRHS)+ (attributes+=attr_list)*;

node_stmt   :  name=DOT_ID (attributes+=attr_list)*;
//node_stmt     :  name=DOT_ID (port=port)? (attributes+=attr_list)*; // see FIXME for port

attribute: name=DOT_ID "=" value=DOT_ID;

attr_stmt  	:  	type=attributetype (attributes+=attr_list)+;

attr_list : "[" (a_list+=a_list)* "]";

a_list: name=DOT_ID ("=" value=DOT_ID)? (",")?;

subgraph : ( "subgraph" name=DOT_ID? )? "{" (stmts+=stmt)* "}";

//FIXME: Generated portImpl class has compilation errors with Helios Xtext
//port : ":" name=DOT_ID ( ":" compass_pt=compass_pt )? | ":" compass_pt=compass_pt;

edgeRHS: (edgeRHS_node | edgeRHS_subgraph);

edgeRHS_node: op=edgeop node=node_id;

edgeRHS_subgraph: op=edgeop subgraph=subgraph;

//node_id  : name=DOT_ID (port=port)?; // see FIXME for port
node_id  : name=DOT_ID;

enum edgeop :
 directed="->"|undirected="--";
 
enum graphtype :
 graph="graph"|digraph="digraph";
 
enum attributetype :
 graph="graph"|node="node"|edge="edge";

enum compass_pt: north="n" | northeast="ne" | east="e" | southeast="se" | south="s" | southwest="sw" | west="w" | northwest="nw";

terminal DOT_ID :
    (('^')?('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*) |
    ('\"' ( '\\' ('b'|'t'|'n'|'f'|'r'|'\"'|'\''|'\\') | !('\"') )* '\"') |
    (('-')?('.'('0'..'9')+ | ('0'..'9')+('.' ('0'..'9')*)? ));
    
    
    
    
    
    
    
    
    
